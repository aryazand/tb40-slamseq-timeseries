rule jbrowse_create:
    output:
        touch("results/jbrowse/create"),
        local(config["jbrowse"]["dir"] + "/index.html"),
    conda:
        "../envs/jbrowse.yml"
    params:
        jbrowse_dir=config["jbrowse"]["dir"],
    message:
        "create jbrowse folder"
    shell:
        """
        jbrowse create {params.jbrowse_dir} --force
        """


# ─────────────────────────────────────────────────────────────────────────────
# 1. Add Assembly
# ─────────────────────────────────────────────────────────────────────────────


rule faToTwoBit_fa:
    input:
        "results/genome/genome.fasta",
    output:
        "results/genome/genome.2bit",
    log:
        "results/genome/genome.fa_to_2bit.log",
    wrapper:
        "v7.1.0/bio/ucsc/faToTwoBit"


rule jbrowse_add_assembly:
    input:
        local(config["jbrowse"]["dir"] + "/index.html"),
    output:
        touch("results/jbrowse/add_assembly"),
        local(config["jbrowse"]["dir"] + "/config.json"),
    conda:
        "../envs/jbrowse.yml"
    resources:
        file_lock=1,
    params:
        s3_url=lambda wc: "{url}/results/genome/genome.2bit".format(
            url=config["jbrowse"]["s3_url"]
        ),
        jbrowse_config=config["jbrowse"]["dir"] + "/config.json",
        extra=config["jbrowse"]["add_assembly"]["extra"],
    message:
        "add genome assembly to jbrowse"
    shell:
        """
        jbrowse add-assembly {params.s3_url} --type twoBit --target {params.jbrowse_config} {params.extra} --force
        """


# ─────────────────────────────────────────────────────────────────────────────
# 2. Add Annotation
# ─────────────────────────────────────────────────────────────────────────────


rule sort_gff:
    input:
        gff="results/genome/genome.gff",
    output:
        gff="results/genome/genome.sorted.gff.gz",
    conda:
        "../envs/jbrowse.yml"
    message:
        "sort gff3"
    shell:
        """
        jbrowse sort-gff {input.gff} | bgzip >{output.gff}
        """


rule index_gff:
    input:
        "results/genome/genome.sorted.gff.gz",
    output:
        "results/genome/genome.sorted.gff.gz.tbi",
    conda:
        "../envs/jbrowse.yml"
    message:
        "index gff3"
    shell:
        """
        tabix {input}
        """


rule jbrowse_add_anno:
    input:
        local(config["jbrowse"]["dir"] + "/config.json"),
    output:
        touch("results/jbrowse/add_anno"),
    conda:
        "../envs/jbrowse.yml"
    resources:
        file_lock=1,
    params:
        s3_url=lambda wc: "{url}/results/genome/genome.sorted.gff.gz".format(
            url=config["jbrowse"]["s3_url"]
        ),
        jbrowse_config=config["jbrowse"]["dir"] + "/config.json",
        extra=config["jbrowse"]["add_anno"]["extra"],
    message:
        "add genome annotation to jbrowse"
    shell:
        """
        jbrowse add-track {params.s3_url} --target {params.jbrowse_config} {params.extra}
        """


# ─────────────────────────────────────────────────────────────────────────────
# 3. Add BigWigs
# ─────────────────────────────────────────────────────────────────────────────


rule jbrowse_add_bw:
    input:
        local(config["jbrowse"]["dir"] + "/config.json"),
    output:
        touch(
            expand(
                "results/jbrowse/{sample}_{strand}_bw",
                sample=samples.index,
                strand=["plus", "minus"]
            )
        ),
    conda:
        "../envs/jbrowse.yml"
    resources:
        file_lock=1,
    params:
        s3_url_plus=expand(
            config["jbrowse"]["s3_url"] + "/results/deeptools/coverage/{sample}.plus.bw",
            sample=samples.index,
        ),
        s3_url_minus=expand(
            config["jbrowse"]["s3_url"] + "/results/deeptools/coverage/{sample}.minus.bw",
            sample=samples.index,
        ),
        jbrowse_config=config["jbrowse"]["dir"] + "/config.json",
        extra=config["jbrowse"]["add_bw"]["extra"],
    message:
        "add plus bw tracks to jbrowse"
    shell:
        """
        for i in {params.s3_url_plus}; do
            jbrowse add-track $i \
                --target {params.jbrowse_config} \
                --name "${{i##*/}}" \
                {params.extra}
        done

        for i in {params.s3_url_minus}; do
            jbrowse add-track $i \
                --target {params.jbrowse_config} \
                --name "${{i##*/}}" \
                --config '{{"displays":[{{"type":"LinearWiggleDisplay","displayId":"my_bw-LinearWiggleDisplay","inverted":true}}]}}' \
                {params.extra}
        done
        """